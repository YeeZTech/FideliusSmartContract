// SPDX-License-Identifier: MIT
pragma solidity ^0.8.19;

import "./MultiSigInterface.sol";

contract MultiSigTools {
    MultiSigInterface public multisig_contract;

    constructor(address _contract) {
        require(_contract != address(0x0));
        multisig_contract = MultiSigInterface(_contract);
    }

    modifier only_signer() {
        require(
            multisig_contract.is_signer(msg.sender),
            "only a signer can call in MultiSigTools"
        );
        _;
    }

    modifier is_majority_sig(uint64 id, string memory name) {
        bytes32 hash = keccak256(abi.encodePacked(msg.sig, msg.data));
        if (
            multisig_contract.update_and_check_reach_majority(
                id,
                name,
                hash,
                msg.sender
            )
        ) {
            _;
        }
    }

    modifier is_majority_sig_with_hash(
        uint64 id,
        string memory name,
        bytes32 hash
    ) {
        if (
            multisig_contract.update_and_check_reach_majority(
                id,
                name,
                hash,
                msg.sender
            )
        ) {
            _;
        }
    }

    event TransferMultiSig(address _old, address _new);

    function transfer_multisig(
        uint64 id,
        address _contract
    ) public only_signer is_majority_sig(id, "transfer_multisig") {
        require(_contract != address(0x0));
        address old = address(multisig_contract);
        multisig_contract = MultiSigInterface(_contract);
        emit TransferMultiSig(old, _contract);
    }
}
